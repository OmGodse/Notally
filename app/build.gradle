plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'com.ncorti.ktfmt.gradle' version '0.20.1'
}

android {
    compileSdk 34
    namespace 'com.omgodse.notally'

    defaultConfig {
        applicationId 'com.omgodse.notally'
        minSdk 21
        targetSdk 33
        versionCode 54
        versionName "5.9"
        resConfigs 'en', 'ca', 'cs', 'da', 'de', 'el', 'es', 'fr', 'hu', 'in', 'it', 'ja', 'my', 'nb', 'nl', 'nn', 'pl', 'pt-rBR', 'pt-rPT', 'ro', 'ru', 'sk', 'sv', 'tl', 'tr', 'uk', 'vi', 'zh-rCN'
        vectorDrawables.generatedDensities = []
    }

    ksp {
        arg("room.generateKotlin", "true")
        arg("room.schemaLocation", "$projectDir/schemas")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            crunchPngs false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    kotlinOptions { jvmTarget = "1.8" }

    buildFeatures { viewBinding true }

    packagingOptions.resources {
        excludes += ["DebugProbesKt.bin", "META-INF/**.version", "kotlin/**.kotlin_builtins", "kotlin-tooling-metadata.json"]
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "1.8"
}

ktfmt {
    kotlinLangStyle()
}

tasks.withType(Test).configureEach {
    dependsOn 'ktfmtFormat'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
    final def navVersion = "2.3.5"
    final def roomVersion = "2.6.1"

    ksp "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"

    implementation "androidx.work:work-runtime:2.9.0"

    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"

    implementation "org.ocpsoft.prettytime:prettytime:4.0.6.Final"
    implementation "com.google.android.material:material:1.4.0"

    implementation 'com.github.zerobranch:SwipeLayout:1.3.1'

    implementation "com.github.bumptech.glide:glide:4.15.1"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core:1.6.1"
    testImplementation "org.mockito:mockito-core:5.13.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
    testImplementation "io.mockk:mockk:1.13.12"
}