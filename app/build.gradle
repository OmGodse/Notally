plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
}

android {
    compileSdk 33
    namespace "com.omgodse.notally"

    defaultConfig {
        applicationId "com.omgodse.notally"
        minSdk 21
        targetSdk 32
        versionCode 47
        versionName "5.2"
        resConfigs "en", "ca", "cs", "da", "de", "el", "es", "fr", "hu", "in", "it", "ja", "nb", "nl", "nn", "pl", "pt-rBR", "pt-rPT", "ru", "sk", "sv", "tl", "tr", "uk", "vi", "zh-rCN"
        vectorDrawables.generatedDensities = []
    }

    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            crunchPngs false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    kotlinOptions { jvmTarget = "1.8" }

    buildFeatures { viewBinding true }

    packagingOptions.resources {
        excludes += ["DebugProbesKt.bin", "META-INF/**.version", "kotlin/**.kotlin_builtins", "kotlin-tooling-metadata.json"]
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    final def navVersion = "2.3.5"
    final def roomVersion = "2.5.1"

    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"

    implementation "androidx.work:work-runtime:2.8.1"

    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"

    implementation "org.ocpsoft.prettytime:prettytime:4.0.6.Final"
    implementation "com.google.android.material:material:1.4.0"

    implementation "ru.rambler.android:swipe-layout:1.1.0"
}